rosesourcecode = { rosecommand , "\n" }

rosecommand =  metadata
			| enumdef
			| entitydef
			| "link"
			| createoperation

			
			
metadata = "set " , ( stringmetadata | boolmetadata )

stringmetadata = stringmetadataproperty , stringvalue

stringmetadataproperty = "srcpath " | "sqlpath " | "dbtype " | "dbuser " | "dbpassword " | "dbserver " | "dbport " | "dbname " 
						| "mainpackage " | "mainname " | "modelpackage " | "parserpackage " | "parserformat "
	
boolmetadata = boolmetadataproperty , boolvalue

boolmetadataproperty = "usingAnnotations " | "usingForeignKeys "

boolvalue = "true" | "false"


createoperation = "create " , createoption

createoption = "sqlcreate" | "hibernate" | "javamodels" | "javaparser" | "javamain"


enumdef = "begin enum " , identifier , "\n" , { identifier , "\n"} , "end enum"


entitydef = "begin entity " , identifier , [ ":" , interfacetype ] , "\n" , { fielddef , "\n" } , "end entity"

interfacetype = "r" | "w"

fielddef = primitivefielddef 
		| enumfielddef 
		| entityfielddef 
		| tostring

primitivefielddef = primitivetype ,  identifier , { defvalue }

primitivetype = "varchar(" , integer , ") " 
			| "char(" , integer , ") "
			| "numeric(" , integer , "," , integer , ") "
			| "int " 
			| "date " 
			| "boolean "

enumfielddef = "enum " , identifier , { identifier , { defvalue } }

entityfielddef = relationtype , identifier , { identifier , { identifier } }

relationtype = "onetoone " | "onetomany " | "manytoone " | "manytomany "

tostring = "tostring " , { stringvalue | "%" , identifier }



integer = an integer

stringvalue = a string without newline

identifier = a string, starting with a letter, no inner whitespace, whitespace at the end

defvalue = corresponding default value, e.g. 123 or "string"
