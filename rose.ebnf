rosesourcecode = { rosecommand , "\n" }

rosecommand =  setmetadata
			| enumdef
			| entitydef
			| createoperation
			| import

import = "import" , identifier
			
setmetadata = "set" , ( stringmetadata | boolmetadata )

stringmetadata = stringmetadataproperty , stringvalue

stringmetadataproperty = "srcpath" | "sqlpath" | "dbtype" | "dbuser" | "dbpassword" | "dbserver" | "dbport" | "dbname" | "modelpackage" | "dateformat"
						| "dtopackage" | "dtocontainername" | "parserpackage" | "parserformat" | "resourcepackage" | "resourcepath" | "retrofitname" | "retrofitpackage"
	
boolmetadata = boolmetadataproperty , boolvalue

boolmetadataproperty = "usingAnnotations" | "usingForeignKeys" | "usingTimestamp" | "usingInterfaces" | "implementDto"


createoperation = "create" , createoption

createoption = "sqlcreate" | "persistence" | "javamodels" | "javaparser" | "rosefilecopy" | "javadtos" | "javadtocontainer" | "javaenums" | "retrofit"


enumdef = "begin enum" , identifier , "\n" , { identifier , "\n"} , "end enum"


entitydef = "begin entity" , identifier , [ ":" , interface ] , "\n" , { fielddecl , "\n" } , "end entity"

interface = "i" | "r" | "w" | "p"

fielddecl = primitivefielddecl 
		| enumfielddecl 
		| entityfielddecl 
		| tostring

primitivefielddecl = primitivetype ,  identifier , [ defvalue ]

primitivetype = "varchar(" , integervalue , ")" 
			| "char(" , integervalue , ") "
			| "numeric(" , integervalue , "," , integervalue , ") "
			| "int" 
			| "date" 
			| "boolean"

enumfielddecl = "enum" , identifier , [ identifier , [ defvalue ] ]

entityfielddecl = relationtype , identifier , [ identifier , [ identifier ] ]

relationtype = "onetoone" | "onetomany" | "manytoone" | "manytomany"

tostring = "tostring" , { stringvalue | "%" , identifier }



boolvalue = "true" | "false"

integervalue = an integer

stringvalue = a string without newline

identifier = a string, starting with a letter, no inner whitespace, whitespace at the end

defvalue = corresponding default value, e.g. 123 or "string"
